; a = 165
; b = 100
; c = 90
; d = 140
; e = 100
; f = 110

prioritet FUNCTION RN1,D5
.2,1/.4,2/.6,3/.8,4/1,5
; дискретная функция(D5 - 5 узловых точек) prioritet,
; аргументом является случайная величина (RN1),
; равномерно распределенная в диапазоне [0,1]

advance_time FUNCTION P1,D5
1,90/2,100/3,100/4,110/5,140
; дискретная функция(D5 - 5 узловых точек) advance_time,
; аргументом является первый параметр транзакта
; параметр 1 принимает значения 1,2,3,4,5
; функция advance_time – 140,110,100,100,90

que	QTABLE Q_kanal,1,1,10
;Q_kanal - номер очереди;
;1 - верхняя граница нижнего частотного интервала;
;1 - ширина интервала;
;10 - число интервалов.
;Создает таблицу, содержащую значения времени пребывания в очереди.

tablica	TABLE M1,1,1,120
;M1 - аргумент таблицы(время жизни активного транзакта)
;1 - верхняя граница нижнего интервала;
;1 - ширина интервала;
;120 - число интервалов;

kanal STORAGE 3

GENERATE(EXPONENTIAL(1,0,165)); a = 165
; распределение - экспоненциальный поток заявок со средним
; интервалом поступления a единиц модельного времени

obrabotka VARIABLE fn$advance_time
; Имя переменной obrabotka. Эта переменная определяется как
; значение выражения fn$advance_time.
; Т.е. передается выбранное время обработки заявки.

ASSIGN 1,fn$prioritet
; присваиваем значение fn$prioritet вместо 1.

PRIORITY p1
; назначаем приоритет транзакта

QUEUE Q_kanal
; становимся в очередь
ENTER kanal
DEPART Q_kanal
ADVANCE V$obrabotka
; задерживается на время обслуживания заявки
LEAVE kanal
TABULATE tablica
; занести значение tablica в таблицу
TERMINATE

GENERATE (480#2#5)
TERMINATE 1
START 1
